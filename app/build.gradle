plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'dagger.hilt.android.plugin'
    id 'kotlin-kapt'
    id "com.google.devtools.ksp" version "1.8.0-1.0.9"
    id "com.mxalbert.gradle.jacoco-android" version "0.2.1" apply false
    id "com.google.protobuf" version "0.9.1"
}

kotlin {
    sourceSets {
        debug {
            kotlin.srcDir("build/generated/ksp/debug/kotlin")
        }
        release {
            kotlin.srcDir("build/generated/ksp/release/kotlin")
        }
    }
}

apply from: "../buildscripts/jacoco.gradle"
apply from: "../buildscripts/coveralls.gradle"

android {
    compileSdk 34

    defaultConfig {
        applicationId "com.onepercentbetter"
        minSdk 24
        minSdkVersion 24
        targetSdk 34
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner = "com.onepercentbetter.HiltTestRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
        }
        debug {
            testCoverageEnabled true
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = "17"
    }
    buildFeatures {
        viewBinding true
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion = "1.4.0"
    }
    applicationVariants.all { variant ->
        kotlin.sourceSets {
            getByName(variant.name) {
                kotlin.srcDir("build/generated/ksp/${variant.name}/kotlin")
            }
        }
    }
    testOptions {
        unitTests.all {
            kover {
                enabled = true
                excludes = [
                        "dagger.hilt.internal.aggregatedroot.codegen.*",
                        "hilt_aggregated_deps.*",
                        "com.onepercentbetter.core.di.*",
                        "com.onepercentbetter.core.ui.theme.*",
                        ".*ComposableSingletons.*",
                        ".*Hilt.*",
                        ".*BuildConfig.*",
                        ".*_Factory.*",
                ]
            }
        }
    }
    namespace 'com.onepercentbetter'
}

dependencies {

    lintChecks(project(":lint-checks"))
    implementation(project(":core-model"))
    implementation(project(":core-data"))
    implementation(project(":task-api"))
    implementation "androidx.core:core-ktx:$rootProject.ext.versions.ktxCore"
    implementation "androidx.appcompat:appcompat:$rootProject.ext.versions.appCompat"
    implementation "com.google.android.material:material:$rootProject.ext.versions.material"
    implementation "androidx.compose.ui:ui:1.4.0-alpha04"
    implementation "androidx.compose.ui:ui-tooling-preview:1.4.0-alpha04"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$rootProject.ext.versions.lifecycle"
    implementation "androidx.activity:activity-compose:$rootProject.ext.versions.activityCompose"
    implementation "com.google.dagger:hilt-android:$hiltVersion"
    implementation "androidx.hilt:hilt-navigation-compose:$rootProject.ext.versions.hiltNavigationCompose"
    implementation "com.google.accompanist:accompanist-insets-ui:$rootProject.ext.versions.accompanist"
    implementation "com.google.accompanist:accompanist-systemuicontroller:$rootProject.ext.versions.accompanist"
    implementation "androidx.compose.foundation:foundation:1.4.0-alpha03"
    implementation "androidx.compose.material3:material3:1.1.0-alpha04"
    implementation "androidx.compose.material3:material3-window-size-class:1.1.0-alpha04"
    implementation "io.github.raamcosta.compose-destinations:animations-core:$rootProject.ext.versions.composeDestinations"
    implementation "androidx.navigation:navigation-compose:$rootProject.ext.versions.composeNavigation"
    implementation "com.google.accompanist:accompanist-navigation-animation:$rootProject.ext.versions.accompanist"
    implementation "io.github.vanpra.compose-material-dialogs:datetime:0.6.2"
    implementation "androidx.room:room-runtime:$rootProject.ext.versions.room"
    implementation "androidx.room:room-ktx:$rootProject.ext.versions.room"
    implementation "androidx.window:window:$rootProject.ext.versions.windowManager"
    implementation "androidx.datastore:datastore:$rootProject.ext.versions.dataStore"
    implementation "com.google.protobuf:protobuf-javalite:3.18.0"
    ksp "androidx.room:room-compiler:$rootProject.ext.versions.room"
    ksp "io.github.raamcosta.compose-destinations:ksp:$rootProject.ext.versions.composeDestinations"
    kapt "com.google.dagger:hilt-compiler:$hiltVersion"
    testImplementation "junit:junit:$rootProject.ext.versions.junit"
    testImplementation "io.mockk:mockk:$rootProject.ext.versions.mockk"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$rootProject.ext.versions.coroutinesTest"
    testImplementation "com.google.truth:truth:$rootProject.ext.versions.truth"
    testImplementation "app.cash.turbine:turbine:$rootProject.ext.versions.turbine"
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:1.4.0-alpha04"
    androidTestImplementation "androidx.test.ext:junit:$rootProject.ext.versions.androidxTestJunit"
    androidTestImplementation "androidx.test.espresso:espresso-core:$rootProject.ext.versions.espresso"
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
    androidTestImplementation "com.google.truth:truth:$rootProject.ext.versions.truth"
    androidTestImplementation "com.google.dagger:hilt-android-testing:$hiltVersion"
    kaptAndroidTest "com.google.dagger:hilt-android-compiler:$hiltVersion"
    debugImplementation "androidx.compose.ui:ui-tooling:1.4.0-alpha04"
    debugImplementation "androidx.compose.ui:ui-test-manifest:1.4.0-alpha04"

    coreLibraryDesugaring "com.android.tools:desugar_jdk_libs:1.1.5"
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.21.7"
    }

    // Generates the java Protobuf-lite code for the Protobufs in this project. See
    // https://github.com/google/protobuf-gradle-plugin#customizing-protobuf-compilation
    // for more information.
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option 'lite'
                }
            }
        }
    }
}
